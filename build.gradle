plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.sonarqube' version '4.2.1.3168'
}

group = 'kr.inventory'
version = '0.0.1-SNAPSHOT'
description = 'inventory'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // AI/MCP
    implementation platform("org.springframework.ai:spring-ai-bom:1.1.0")
    implementation 'org.springframework.ai:spring-ai-starter-mcp-server-webmvc'

    // Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // AOP
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // 모니터링 - Prometheus/Grafana
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // Logback JSON 형식 지원
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

    // Security / OAuth
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // DB
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

    // Docs
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15"

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:5.0.0:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // JPA / Validation
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Lombok / Dev
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Testcontainers
    testImplementation 'org.testcontainers:testcontainers:1.19.7'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.7'
    testImplementation 'org.testcontainers:postgresql:1.19.7'
    testImplementation 'com.redis:testcontainers-redis:2.2.2'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

    // Env
    implementation 'me.paulschwarz:springboot3-dotenv:5.1.0'

    // AWS S3
    implementation platform('software.amazon.awssdk:bom:2.25.11')
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:sts'

    // PDFBox
    implementation 'org.apache.pdfbox:pdfbox:3.0.1'

    // Apache POI
    // Source: https://mvnrepository.com/artifact/org.apache.poi/poi
    implementation("org.apache.poi:poi:5.5.1")
}

jacoco {
    toolVersion = '0.8.12'
}

def qDomains = []
for (qPattern in '*.QA'..'*.QZ') {
    qDomains.add(qPattern + '*')
}

def jacocoExcludePatterns = [
        // 측정 안하고 싶은 패턴
        '**/*Application*',
        '**/*Config*',
        '**/*Exception*',
        '**/*ErrorCode*',
        '**/*Request*',
        '**/*Response*',
        '**/*Dto*',
        '**/*Enum*',
        '**/*Interceptor*',
        '**/*Filter*',
        '**/*Resolver*',

        // QueryDSL generated
        '**/generated/**',
        '**/Q*'
]

def applyJacocoClassDirectoriesExcludes = { task ->
    task.classDirectories.setFrom(
            files(task.classDirectories.files.collect {
                fileTree(dir: it, excludes: jacocoExcludePatterns + qDomains)
            })
    )
}

sonar {
    properties {
        property('sonar.projectKey', 'prgrms-aibe-devcourse_AIBE4_FinalProject_Team1_BE')
        property('sonar.organization', 'prgrms-aibe-devcourse')
        property('sonar.host.url', 'https://sonarcloud.io')

        property('sonar.sources', 'src/main/java')
        property('sonar.tests', 'src/test/java')
        property('sonar.java.binaries', 'build/classes/java/main')
        property('sonar.sourceEncoding', 'UTF-8')

        property('sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml')
        property('sonar.exclusions', (jacocoExcludePatterns + qDomains).join(','))
    }
}

// Querydsl 빌드 옵션 설정
def generated = 'src/main/generated'

// Querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// java source set 에 Querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [generated]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
    delete file(generated)
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy tasks.named('jacocoTestReport')
}

tasks.named("sonar") {
    dependsOn(tasks.named("jacocoTestReport"))
}

jacocoTestReport {
    dependsOn test

    def reportTask = delegate

    reports {
        html.required.set(true)
        xml.required.set(true)
        xml.outputLocation.set(layout.buildDirectory.file('reports/jacoco/test/jacocoTestReport.xml'))
        csv.required.set(false)
    }

    afterEvaluate {
        applyJacocoClassDirectoriesExcludes(reportTask)
    }

    // Coverage gate is run explicitly in CI so Sonar scan still runs
}

jacocoTestCoverageVerification {
    dependsOn test

    def verificationTask = delegate

    violationRules {
        rule {
            enabled = true

            // 전체 번들 기준으로 커버리지 체크 (클래스별 0%로 과도하게 실패하는 것 방지)
            element = 'BUNDLE'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.20
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.20
            }

            excludes = jacocoExcludePatterns + qDomains
        }
    }

    afterEvaluate {
        applyJacocoClassDirectoriesExcludes(verificationTask)
    }
}

tasks.named('check') {
    dependsOn tasks.named('jacocoTestCoverageVerification')
}
